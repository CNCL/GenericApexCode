/*
    This is a generic batch which can be used to update on any objects 
    
    How to execute this batch 
    ***************************
    string query='SELECT DormantDate__c,Dormant__c,Id,LastTradeDate__c FROM ADSS_Platform_Account__c';
    Map<String, String> sfldValues = new Map<String, String>();
    Map <String,Date> dfldValues = new Map <String,Date>();
    Map <String,Boolean> bfldValues = new Map <String,Boolean>();
    sfldValues.put('negotiated_profile__c','Test2');
    bfldValues.put('Dormant__c', True);
    dfldValues.put('DormantDate__c' , Date.today());
    // and so on for all fields you want to udpate
    batchupdatefield batch=new batchupdatefield(query,sfldValues,dfldValues,bfldValues);
    ------- currently due to system inefficiency we can run the batch with a batch size of 50 only
    Database.executebatch(batch,50);
    ****************************
*/


public class BatchUpdateField implements Database.Batchable <sObject> {
	public final String Query;
	public final Map <String,String> stringFieldValues;
	public final Map <String,Date> dateFieldValues;
	public final Map <String,Boolean> booleanFieldValues;
	public BatchUpdateField(String q, 
	                        Map <String,String> stringFieldValues,
	                        Map <String,Date> dateFieldValues,
	                        Map <String,Boolean> booleanFieldValues
	                        ) {
		Query = q;
		this.stringFieldValues = stringFieldValues;
		this.dateFieldValues = dateFieldValues;
		this.booleanFieldValues = booleanFieldValues;
	}
	public Database.QueryLocator start(Database.BatchableContext BC) {
		return Database.getQueryLocator(query);
	}
	public void execute(Database.BatchableContext BC, List <sObject> scope) {
		for (sobject s: scope) {
			for (String fldName: stringFieldValues.keySet()) {
				s.put(fldName, stringFieldValues.get(fldName));
			}
			for (String fldName: dateFieldValues.keySet()) {
				s.put(fldName, dateFieldValues.get(fldName));
			}
			for (String fldName: booleanFieldValues.keySet()) {
				s.put(fldName, booleanFieldValues.get(fldName));
			}
		}

		update scope;
	}
	public void finish(Database.BatchableContext BC) {
		AsyncApexJob a = [Select Id, Status, NumberOfErrors, JobItemsProcessed, TotalJobItems, CreatedBy.Email
			from AsyncApexJob where Id =: BC.getJobId()];
		string message = 'The batch Apex job processed ' + a.TotalJobItems + ' batches with ' + a.NumberOfErrors + ' failures.';
		// Send an email to the Apex job's submitter notifying of job completion.
		Messaging.SingleEmailMessage mail = new Messaging.SingleEmailMessage();
		String[] toAddresses = new String[] {
		    a.CreatedBy.Email
		};
		mail.setToAddresses(toAddresses);
		mail.setSubject('Salesforce BatchUpdateField ' + a.Status);
		mail.setPlainTextBody('The batch Apex job processed ' + a.TotalJobItems + ' batches with ' + a.NumberOfErrors + ' failures.');
		Messaging.sendEmail(new Messaging.SingleEmailMessage[] {
			mail
		});
	}
}
